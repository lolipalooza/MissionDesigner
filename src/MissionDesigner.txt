{======================================================================
======================================================================}
{$CLEO .s}
thread 'MisDesigner'

// Change version here
0ADF: add_dynamic_GXT_entry "MDVERX" text "v 0.1.1"

gosub @DesignerMain
0A93: end_custom_thread

{======================================================================
======================================================================}
const
    PAD_FIREWEAPON = 17
end

{======================================================================
======================================================================}
// Common
{$I common/common.txt}
{$I common/CursorControl.txt}

// Interface Designer
{$I InterfaceDesigner/InterfaceDesigner.txt}
{$I InterfaceDesigner/DrawnTexts.txt}

{======================================================================
======================================================================}
:DesignerMain

// Initialize
$ONMISSION = 1
0581: enable_radar 0
0826: enable_hud 0
03F0: enable_text_draw 1
01B4: set_player $PLAYER_CHAR can_move 0
0AB1: @InitCursorControl 0

// Main Loop
repeat
wait 0
    
    0AB1: @CursorControl 0
    
    if gosub @DesignerMachine
    then break
    end
    
until false

repeat
wait 0
until 80E1:   not player 0 pressed_key 15 and 80E1:   not player 0 pressed_key 17

// Finalize
0581: enable_radar 1
0826: enable_hud 1
03F0: enable_text_draw 0
01B4: set_player $PLAYER_CHAR can_move 1
$ONMISSION = 0
return

{======================================================================
======================================================================}
:MainStat
hex
0
end

{======================================================================
======================================================================}
:DesignerMachine
0AB1: @GetInt 1 @MainStat ret 10@

059A:  return_false

if 10@ == 0
then
    0AB1: @MainMenu_Show 0 ret clicked_button 11@

    // :MainMenu_Control
    if 11@ == 1
    then 0AD0: show_formatted_text_lowpriority "Mision Builder. Not ready yet..." time 750
    end
    if 11@ == 2
    then 10@ = 2 // Interface Designer Menu
    end
    if 11@ == 3
    then 0AD0: show_formatted_text_lowpriority "Director Mode. Not ready yet..." time 750
    end
    if 11@ == 4
    then 0AD0: show_formatted_text_lowpriority "Storyline Creator. Not ready yet..." time 750
    end
    if or
    11@ == 5
    00E1:   player 0 pressed_key 15 // F
    then
        0485:  return_true
        return
    end
end

if 10@ == 1 // Mission Builder Menu
then
end

if 10@ == 2 // Interface Designer Menu
then
    0AB1: @InterfaceDesignerMenu_Show 0 ret clicked_button 11@    
    0AB1: @DisplayDrawnTexts 0
    0AB1: @DisplayEditTextMenu 0
    
    // :InterfaceDesignerMenu_Control
    if 11@ == 2
    then 0AD0: show_formatted_text_lowpriority "Compact version of the menu. Not ready yet..." time 750
    end
    if 11@ == 3
    then 10@ = 5 // Create interface Menu
    end
    if 11@ == 4
    then 0AD0: show_formatted_text_lowpriority "Load interface. Not ready yet..." time 750
    end
    if 11@ == 5
    then 0AD0: show_formatted_text_lowpriority "Delete interface. Not ready yet..." time 750
    end
    if or
    11@ == 1
    00E1:   player 0 pressed_key 15 // F
    then
        repeat
        wait 0
        until 80E1:   not player 0 pressed_key 15 // F
        10@ = 0 // Return to Main Menu
    end
end

if 10@ == 3 // Director Mode Menu
then
end

if 10@ == 4 // Storyline Creator Menu
then
end

{======================================================================
// Interface designer menus
======================================================================}
if 10@ == 5 // Create interface Menu
then
    0AB1: @CreateInterfaceMenu_Show 0 ret clicked_button 11@    
    0AB1: @DisplayEditTextMenu 0
    0AB1: @DisplayDrawnTexts 0
    
    // :CreateInterfaceMenu_Control
    if 11@ == 2
    then 0AD0: show_formatted_text_lowpriority "Compact version of the menu. Not ready yet..." time 750
    end
    if 11@ == 3
    then 0AD0: show_formatted_text_lowpriority "New box/texture. Not ready yet..." time 750
    end
    if 11@ == 4
    then //0AD0: show_formatted_text_lowpriority "New text. Not ready yet..." time 750
        0AB1: @CreateNewText 0
        //0AB1: @DEBUG_DumpTextsMemory 0
    end
    if 11@ == 5
    then 0AD0: show_formatted_text_lowpriority "New button. Not ready yet..." time 750
    end
    if 11@ == 6
    then 0AD0: show_formatted_text_lowpriority "New array. Not ready yet..." time 750
    end
    if 11@ == 7
    then 0AD0: show_formatted_text_lowpriority "Save. Not ready yet..." time 750
    end
    if 11@ == 8
    then 0AD0: show_formatted_text_lowpriority "Export plain text. Not ready yet..." time 750
    end
    if or
    11@ == 1
    00E1:   player 0 pressed_key 15 // F
    then
        repeat
        wait 0
        until 80E1:   not player 0 pressed_key 15 // F
        10@ = 2 // Return to Interface Designer Menu
        0AB1: @StoreInt 3 label @DrawnTextInUse value 0xFF offset 0 // Exit edit mode
    end
end

if 10@ == 99 // Dummy state (does nothing but prevents falses "return_true")
then
end

0AB1: @StoreInt 2 @MainStat 10@
return

{======================================================================
======================================================================}
:MainMenu_Show
// Title
10@s = 'MSDESGN' // Mission Designer
0AB1: @SetDrawText 9 behind_textures 1 font 0 linewidth 600.0 position 24.0 195.0 size 0.8 2.7 gxt 10@ 11@

10@s = 'MDVERX' // v ~1~.~1~
0AB1: @SetDrawText 9 behind_textures 1 font 1 linewidth 600.0 position 190.0 214.0 size 0.3 1.2 gxt 10@ 11@

// Options
10@s = 'MSBUILD' // Mision Builder
0AB1: @SetDrawText 9 behind_textures 1 font 1 linewidth 600.0 position 45.0 26.0 size 0.3 1.0 gxt 10@ 11@

10@s = 'INTDES' // Interface Designer
0AB1: @SetDrawText 9 behind_textures 1 font 1 linewidth 600.0 position 45.0 50.0 size 0.3 1.0 gxt 10@ 11@

10@s = 'DIRMODE' // Director Mode
0AB1: @SetDrawText 9 behind_textures 1 font 1 linewidth 600.0 position 45.0 75.0 size 0.3 1.0 gxt 10@ 11@

10@s = 'STCRTR' // Storyline Creator
0AB1: @SetDrawText 9 behind_textures 1 font 1 linewidth 600.0 position 45.0 101.0 size 0.3 1.0 gxt 10@ 11@

10@s = 'FNSH' // Finish
0AB1: @SetDrawText 9 behind_textures 1 font 2 linewidth 600.0 position 123.0 136.0 size 0.3 1.2 gxt 10@ 11@

// The box is nothing more than a texture, the antialiased textures show always behind texts
// and the not antialiased textures only show above texts if texts are commanded to through the opcode 03E0: draw_text_behind_textures 1
03E3: set_texture_to_be_drawn_antialiased 1
038E: draw_box_position 99.0 131.0 size 156.0 262.0 RGBA 0 0 0 100
03E3: set_texture_to_be_drawn_antialiased 1
038E: draw_box_position 89.0 131.0 size 136.0 262.0 RGBA 0 0 0 100
03E3: set_texture_to_be_drawn_antialiased 1
038E: draw_box_position 79.0 131.0 size 116.0 262.0 RGBA 0 0 0 100
03E3: set_texture_to_be_drawn_antialiased 1
038E: draw_box_position 69.0 131.0 size 96.0 262.0 RGBA 0 0 0 100
03E3: set_texture_to_be_drawn_antialiased 1
038E: draw_box_position 140.0 178.0 size 269.0 5.0 RGBA 255 0 72 140

03E3: set_texture_to_be_drawn_antialiased 1
038E: draw_box_position 132.0 201.0 size 220.0 14.0 RGBA 255 0 50 150
03E3: set_texture_to_be_drawn_antialiased 1
038E: draw_box_position 120.0 212.0 size 210.0 47.0 RGBA 255 0 50 120
03E3: set_texture_to_be_drawn_antialiased 1
038E: draw_box_position 124.0 209.0 size 218.0 41.0 RGBA 255 0 50 120
03E3: set_texture_to_be_drawn_antialiased 1
038E: draw_box_position 129.0 205.0 size 226.0 33.0 RGBA 255 0 50 120

// Control
if 0AB1: @CursorClickCheck 9 box_position 98.0 31.0 box_size 140.0 16.0 hover_rgba 22 85 145 120 item_offset 0 // Mision Builder
then 0AB2: ret 1 clicked_button 1
end
if 0AB1: @CursorClickCheck 9 box_position 98.0 55.0 box_size 140.0 16.0 hover_rgba 22 85 145 120 item_offset 1 // Interface Designer
then 0AB2: ret 1 clicked_button 2
end
if 0AB1: @CursorClickCheck 9 box_position 98.0 80.0 box_size 140.0 16.0 hover_rgba 22 85 145 120 item_offset 2 // Director Mode
then 0AB2: ret 1 clicked_button 3
end
if 0AB1: @CursorClickCheck 9 box_position 98.0 107.0 box_size 140.0 16.0 hover_rgba 22 85 145 120 item_offset 3 // Storyline Creator
then 0AB2: ret 1 clicked_button 4
end
if 0AB1: @CursorClickCheck 9 box_position 127.0 143.0 box_size 100.0 24.0 hover_rgba 22 85 145 120 item_offset 4 // Finish
then 0AB2: ret 1 clicked_button 5
end

0AB2: ret 1 clicked_button 0

